plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'androidx.navigation.safeargs'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'kotlinx-serialization'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
}

android {
    def target = 33
    compileSdkVersion target
    defaultConfig {
        applicationId "com.softklass.warbler"
        minSdkVersion 26
        targetSdkVersion target
        versionCode 13
        versionName "0.13"
        testInstrumentationRunner "com.warbler.config.HiltAndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
        vectorDrawables.useSupportLibrary = true

        buildConfigField("String", "WEATHER_BASE_URL", "\"${System.getenv('WEATHER_BASE_URL') ?: getProperty("WEATHER_BASE_URL")}\"")
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "WEATHER_API_KEY", "\"${System.getenv('WEATHER_API_KEY') ?: getProperty("WEATHER_API_KEY")}\"")
            signingConfig signingConfigs.debug
        }
        debug {
            minifyEnabled false
            buildConfigField("String", "WEATHER_API_KEY", "\"${System.getenv('WEATHER_API_KEY') ?: getProperty("WEATHER_API_KEY")}\"")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled true
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }
    namespace 'com.warbler'

    android.packagingOptions {
        exclude 'META-INF/*'
    }
}

ktlint {
    android = true
    ignoreFailures = false
    reporters {
        reporter "plain"
        reporter "checkstyle"
        reporter "sarif"
    }
}

tasks.getByPath("preBuild").dependsOn("ktlintFormat")

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //  Android specific
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'com.google.android.gms:play-services-location:21.0.1'

    // DI
    implementation "com.google.dagger:hilt-android:$dagger_hilt"
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    kapt "com.google.dagger:hilt-android-compiler:$dagger_hilt"
    kapt "androidx.hilt:hilt-compiler:$hilt_lifecycle_viewmodel"

    //Firebase
    // Import the BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:32.1.1')

    // Add the dependencies for the Crashlytics and Analytics libraries
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    // Add the dependency for the Realtime Database library
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-database-ktx'

    // Add the dependency for the Firebase SDK for Google Analytics
    implementation 'com.google.firebase:firebase-analytics-ktx'

    // Add Performance lib
    implementation 'com.google.firebase:firebase-perf-ktx'

    // In App Updates
    implementation 'com.google.android.play:app-update:2.1.0'
    // For Kotlin users also add the Kotlin extensions library for Play In-App Update:
    implementation 'com.google.android.play:app-update-ktx:2.1.0'

    // For instrumented tests.
    androidTestImplementation "com.google.dagger:hilt-android-testing:$dagger_hilt"
    // ...with Kotlin.
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$dagger_hilt"

    //  Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine"

    // Kotlin
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.1"

    // Feature module Support
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$navigation"

    // Testing Navigation
    androidTestImplementation "androidx.navigation:navigation-testing:$navigation"

    //  data
    implementation "androidx.room:room-runtime:$room"
    implementation "androidx.room:room-ktx:$room"
    kapt "androidx.room:room-compiler:$room"

    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation "androidx.datastore:datastore-preferences-core:1.0.0"


    // network
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit"
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:1.0.0"
    implementation "com.squareup.okhttp3:okhttp:4.11.0"
    implementation "androidx.browser:browser:1.5.0"

    //  testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'com.google.android.apps.common.testing.accessibility.framework:accessibility-test-framework:4.0.0'
    androidTestImplementation 'com.github.JoshLudahl:espresso-library-kotlin:0.14.0'

    androidTestUtil 'androidx.test:orchestrator:1.4.2'
}
